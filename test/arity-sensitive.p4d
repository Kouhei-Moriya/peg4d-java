File = Chunk

Chunk
    = { @Statement (_ [\n\r]+ _ @Statement)+ #Source }
    / Statement

SP  = [ \t]+
_   = [ \t]*

W0 = [a-zA-Z_]
W = [a-zA-Z0-9_]

"def" = 'def' !W

Statement
    = Function / Apply

Expression
    = Apply / Name

Name
    = { W0 W* #Name }

Param
    = { W0 W* #Param }

Function
    = Function3 / Function2 / Function1 / Function0

Function0
    = { "def" _ <name F0 @Name> #Function }

Function1
    = { "def" _ <name F1 @Name> _ @Param #Function }

Function2
    = { "def" _ <name F2 @Name> _ @Param _ @Param #Function }

Function3
    = { "def" _ <name F3 @Name> _ @Param _ @Param _ @Param #Function }

Apply
    = Apply0 / Apply1 / Apply2 / Apply3

Apply0
    = { @{ <isa F0> #Name } #Apply }

Apply1
    = { @{ <isa F1> #Name } _ @Expression #Apply }

Apply2
    = { @{ <isa F2> #Name } _ @Expression _ @Expression #Apply }

Apply3
    = { @{ <isa F3> #Name } _ @Expression _ @Expression _ @Expression #Apply }


